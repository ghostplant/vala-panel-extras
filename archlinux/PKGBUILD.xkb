# Maintainer: rilian-la-te <ria.freelander@gmail.com>

_pkgname=vala-panel-extras
pkgname=${_pkgname}-xkb-git
_cmakename=cmake-vala
pkgver=0.1.0
pkgrel=3
pkgdesc="Simple keyboard StatusNotifierItem"
url="https://github.com/rilian-la-te/vala-panel-extras"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('gtk3' 'libxkbcommon-x11>=0.5.0' 'libxcb>=1.11')
makedepends=('cmake' 'vala')
provides=('vala-panel-extras-xkb')
optdepends=('setxkbmap: for configuring layout'
            'xfce4-sntray-plugin: for showing in Xfce'
            'vala-panel: panel with builtin StatusNotifierItems support'
            'vala-panel-extras-translations: translations'
            'xfce4-snw-plugin: for showing in Xfce, alternate way'
            'enlightenment: for showing in E19'
            'indicator-application: for showing in Indicator Environment, like Pantheon or Unity'
            'plasma-desktop: for showing in KDE Frameworks'
            'kdebase-plasma: for showing in KDE4')
source=("git://github.com/rilian-la-te/${_pkgname}.git"
        "git://github.com/rilian-la-te/${_cmakename}.git")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  ( set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  cd "${srcdir}/${_cmakename}"
  cp -r . "${srcdir}/${_pkgname}/cmake"
}

build() {
  cd "${srcdir}/${_pkgname}"
  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr -DENABLE_WEATHER=OFF -DENABLE_ALSA=OFF -DENABLE_XKB=ON -DENABLE_BATTERY=OFF
  make
}

package() {
  cd "${srcdir}/${_pkgname}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/share/locale
}


